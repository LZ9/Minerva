plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

android {
    compileSdk 33

    defaultConfig {
        applicationId "com.lodz.android.minervademo"
        minSdk 21
        targetSdk 33
        versionCode 1
        versionName "1.0"
//        ndk {
//            abiFilters "armeabi"
//        }
    }

    viewBinding {
        enabled = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    //日志开关
    def LOG_DEBUG = "LOG_DEBUG"
    //FileProvider
    def AUTHORITY = "AUTHORITY"

    defaultConfig {
        buildConfigField "boolean", LOG_DEBUG, "true"
        buildConfigField "String", AUTHORITY, "\"${defaultConfig.applicationId}.fileprovider\""
    }

    // 程序在编译的时候会检查lint，有任何错误提示会停止build，我们可以关闭这个开关

    buildTypes {

        debug {//debug版本的配置
            minifyEnabled false // 是否进行混淆
            zipAlignEnabled false // 是否支持zip
            debuggable true // 是否支持调试
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        release {
            buildConfigField "boolean", LOG_DEBUG, "false"
            minifyEnabled false // 是否进行混淆
            zipAlignEnabled true // 是否支持zip
            debuggable false // 是否支持调试
            jniDebuggable false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    lint {
        abortOnError false
        checkReleaseBuilds false
    }
    namespace 'com.lodz.android.minervademo'

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def newName
            if (variant.baseName.endsWith('release')) {
                def formattedDate = new Date().format('yyyyMMddHHmm')
                newName = "Minerva-v" + defaultConfig.versionName + "-" + variant.baseName + "-" + formattedDate + ".apk"
            } else {
                newName = "Minerva-v" + defaultConfig.versionName + "-" + variant.baseName + ".apk"
            }
            outputFileName = newName
        }
    }
}

dependencies {
    implementation 'ink.lodz:pandora:2.0.3'
    implementation "com.github.permissions-dispatcher:ktx:1.1.4"

    implementation 'com.yanzhenjie.permission:support:2.0.1'
    implementation project(path: ':minerva')
}